// johan munoz 	js.munoz.ai@gmail.com

 
//Control de potencia en aire acondicionado


FUNCTION_BLOCK potencia

// Define input variables
VAR_INPUT
    area : REAL;
    personas : REAL;
    temperatura : REAL;
END_VAR

// Define output variable
VAR_OUTPUT
    potencia : REAL;
END_VAR

// Fuzzify variable de entrada area
FUZZIFY area
    TERM pequena := (0,1) (10,1) (20,0); 
    TERM mediana := (15,0) (20,1) (30,1) (35,0);
    TERM grande := (33,0) (40,1) (50,1);
END_FUZZIFY

// Fuzzify variable de entrada personas
FUZZIFY personas
    TERM pocas := (0,1) (2,1) (4,0);
    TERM algunas := (2,0) (5,1) (6,0.5) (8,1) (9,0);
    TERM muchas := (7,0) (9,1) (13,1);
END_FUZZIFY

// Fuzzify variable de entrada temperatura
FUZZIFY temperatura
    TERM frio := (0,1) (10,1) (20,0);
    TERM templado := (15,0) (18,1) (23,1) (28,0);
    TERM caliente := (25,0) (28,1) (32,1);
END_FUZZIFY

// Defzzzify variable de salida potencia
DEFUZZIFY potencia
    TERM baja := (0,0) (5,1) (10,0);
    TERM media := (7,0) (15,1) (23,0);
    TERM alta := (20,0) (25,1) (30,0);
    // Use 'Center Of Gravity' defuzzification method
    METHOD : COG;
    // Default value is 0 (if no rule activates defuzzifier)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max'
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    ACT : MIN;  // Use 'min' activation method
    ACCU : MAX; // Use 'max' accumulation method

    RULE 1 : IF area IS pequena AND personas IS pocas 
                THEN potencia IS baja;
    RULE 2 : IF temperatura IS frio AND personas IS pocas 
                THEN potencia IS baja;
    
    RULE 3 : IF area IS mediana AND personas IS algunas
                THEN potencia IS media;
    RULE 4 : IF temperatura IS templado AND personas IS algunas
                THEN potencia IS media;
    
    RULE 5 : IF area IS grande AND personas IS muchas
                THEN potencia is alta;
    RULE 6 : IF temperatura IS caliente AND personas IS muchas
                THEN potencia is alta;
    
END_RULEBLOCK

END_FUNCTION_BLOCK
